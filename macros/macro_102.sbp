'!FABMO!name:Measure Tool
'!FABMO!description:Determine the current tool's length with the pressure plate
SC,0 ' Table base coordinates
&backoff = 0.25

&ToolToLoad = $CurrentTool
GOSUB LoadToolValues

GOSUB MoveClearOfToolBar



' Probe Z (Fast)
PZ,$Tool1Z-3.0,0.5,1
JZ,%(3)+&backoff

' Probe Z (Slow)
PZ,%(3)-2*&backoff,0.1,1

' Measure Z and calculate length
&ToolLength = %(3)

&ToolToSave = $CurrentTool
GOSUB SaveToolValues

JZ,0
END

LoadToolValues:
  &ToolZ = $Tool1Z
  IF &ToolToLoad == 1 THEN GOTO ToolValuesLoaded
  &ToolZ = $Tool2Z
  IF &ToolToLoad == 2 THEN GOTO ToolValuesLoaded
  &ToolZ = $Tool3Z
  IF &ToolToLoad == 3 THEN GOTO ToolValuesLoaded
  &ToolZ = $Tool4Z
  IF &ToolToLoad == 4 THEN GOTO ToolValuesLoaded
  &ToolZ = $Tool5Z
  IF &ToolToLoad == 5 THEN GOTO ToolValuesLoaded
  &ToolZ = $Tool6Z
  IF &ToolToLoad == 6 THEN GOTO ToolValuesLoaded
  END "Unknown tool."
ToolValuesLoaded:
  	RETURN
    
SaveToolValues:
	IF &ToolToSave == 1 THEN GOTO SaveTool1
	IF &ToolToSave == 2 THEN GOTO SaveTool2
	IF &ToolToSave == 3 THEN GOTO SaveTool3
	IF &ToolToSave == 4 THEN GOTO SaveTool4
	IF &ToolToSave == 5 THEN GOTO SaveTool5
	IF &ToolToSave == 6 THEN GOTO SaveTool6
	END
    
    SaveTool1:
    	$Tool1Length = &ToolLength
    	GOTO ToolValuesSaved
    SaveTool2:
    	$Tool2Length = &ToolLength
    	GOTO ToolValuesSaved
    SaveTool3:
    	$Tool3Length = &ToolLength
    	GOTO ToolValuesSaved
    SaveTool4:
    	$Tool4Length = &ToolLength
    	GOTO ToolValuesSaved
    SaveTool5:
    	$Tool5Length = &ToolLength
    	GOTO ToolValuesSaved
    SaveTool6:
    	$Tool6Length = &ToolLength
    	GOTO ToolValuesSaved

	ToolValuesSaved:
    	RETURN
        

MoveClearOfToolBar:
  ' Position above the pressure plate (
  JZ,0
  IF %(1) <= $Tool1X - 1.5 THEN GOTO MoveY
  IF %(1) >= $Tool1X + 1.5 THEN GOTO MoveY
  JX,$Tool1X+1.5
  RETURN
  MoveY:
	JY,$Tool1Y-$ToolBarPitch
	PAUSE 0.1
	JX,$Tool1X
	PAUSE 0.1
    RETURN